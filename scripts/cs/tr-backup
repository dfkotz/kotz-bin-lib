#!/bin/csh -f
#
# tr-backup:  back up the tech-report database.
#   Each time we are run, we copy any TR files that are new or have
# changed since the last time we were run.
#
# Note that this means we may have multiple copies of a particular 
# tech report; that's ok.  It changed for some reason, and this
# directory maintains a copy of every version of the TR. 
# (We've had problems with the TRs getting clobbered somehow.)
#
# This is designed to work if it is run once per day, or less.
# No changes should be made to TR directory while this is running

set dbackup=/u/dfk/TR-backup
set dtr=/u/trdata/reports
cd $dtr # tickle the file system to be sure it's mounted
cd

set last=$dbackup/last-run

set newfiles=`find $dtr/ -type f  -newer $last \( -name 'TR*.pdf' -o -name 'TR*.ps.Z' \)`

touch $last

if ($#newfiles == 0) exit 0

echo "Tech Report backup"
set today=`date +%Y.%m.%d`

foreach file ($newfiles)
   echo cp $file $dbackup/$today.$file:t
   cp $file $dbackup/$today.$file:t
end
