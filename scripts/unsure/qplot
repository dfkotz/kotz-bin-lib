#!/bin/csh -f
#
# qplot - plot a Quantile plot of a given data set
#
# The data set is presented to the standard input or a set of files,
# one datum per line. They will be sorted if not already sorted.
# The set of files represents ONE data set and will make ONE plot.
#
# usage:
#   qplot [-s] [-t title] [file]...
#
# specifying -s indicates that the data are sorted already
#

set P=~/bfly/rf/driver/Process

onintr cleanup
set data=/tmp/qplot-sorted
set quantiles=/tmp/qplot
unset sorted

while ($1 =~ -*)
    switch("$1")
    case -t:
        # extract optional title
        shift 
        if ($#argv > 0) then
        	  set title="$1"
            shift
        else
        	   echo Usage: '[-t title]'
        endif
    	   breaksw
    case -s:
    	   set sorted
        shift
    	   breaksw
    default:
    	   echo Unknown option "$1".
    endsw
end


# Collect the sorted data; notice $* will be null if there are no
# files and thus we'll use stdin
if ($?sorted) then
	cat $* > $data
else
	sort +0n -1 $* > $data
endif

if (! $?title) set title="Quantile plot of $*"

# Count the number of items
set n=`cat $data | stats n`

# Compute the quantile for each point
dm '(INLINE-.5)/'$n x1 < $data > $quantiles

if ($n < 100) then
	set style=points
else
	set style=dots
endif

cp $quantiles qpoints

lasergnu -l -t "$title" "set samples $n" 'plot [0:1] "'$quantiles'" w '$style

cleanup:
rm -f $data $quantiles
