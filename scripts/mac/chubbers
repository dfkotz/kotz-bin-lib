#!/bin/csh
#
# Send the chubbers mailing list to the mailing list.
# Retrieve the list from listserv, 
# then pipe the response through this script.
#
# usage:
#   chubbers < listserv-output

cd ~/lib/mac/chubbers

onintr cleanup

##################
# Prepare this month's report as chubbers-clean

echo "CHUBBERNET as of "`date +%D` > chubbers-clean
echo "  Name                         Email    " >> chubbers-clean
echo "----------------------------------------" >> chubbers-clean

# Make sure that it has Unix, not Macintosh, line terminators,
# remove non-printing characters resulting from Mail.app use,
# Remove everything in mail header, blank lines, and listserv info,
# Reformat to align email addresses,
# Sort by last name (second name).
tr \\r \\n \
| tr -d '[\302\240]' \
| tr -s '[:blank:]' ' '  \
| sed  -e '/^[A-Z][a-z]*: /d' -e '/^$/d' -e '/^\*/d' -e 's/"//g' \
| awk '{ printf "%-30s %s\n", gensub(/^[^ ]+ */, "", 1), $1 }' \
| sort -f -k2 >> chubbers-clean

# sanity check: it should have a substantial number of addresses!
# (stop now if we get empty or corrupt input!)
set count =  `grep @ chubbers-clean | wc -l`
if ( "$count" < 50 ) then
    echo 'ERROR: input does not look like what I expect.'
    echo 'chubbers-clean now contains the following (top 10 lines):'
    head chubbers-clean
    goto cleanup
endif

#################
# Diff with last month's list
echo "" > chubbers-diffs
echo "Changes since last month (if any) (- left, + joined):" >>chubbers-diffs
diff chubbers-old chubbers-clean | sed -e '/^[-0-9]/d' -e 's/^</-/' -e 's/^>/+/' >> chubbers-diffs

echo "" > chubbers-blank

cat chubbers-{intro,clean,diffs,notes,blank,extras} > chubbers-message

set subject="Monthly ChubberNet Update"
set extras=`extract-emails chubbers-extras`

# email to the list and to each of the extras
foreach address (chubbers@dartmouth.edu $extras)
    echo email to "$address"
    mutt -s "$subject" "$address" < chubbers-message
end

mv -f chubbers-clean chubbers-old

cleanup:
rm -f chubbers-blank chubbers-clean chubbers-diffs chubbers-message
