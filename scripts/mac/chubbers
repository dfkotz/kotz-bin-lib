#!/bin/csh
#
# Send the chubbers mailing list to the mailing list.
# Retrieve the list from listserv, 
# then pipe the response through this script.
#
# usage:
#   chubbers < listserv-output

cd ~/lib/laptop/chubbers

onintr cleanup

#########################################
# Prepare this month's report as list-new

echo "CHUBBERNET as of "`date +%D` > list-new
echo "  Name                         Email    " >> list-new
echo "----------------------------------------" >> list-new

# Make sure that it has Unix, not Macintosh, line terminators,
# remove non-printing characters resulting from Mail.app use,
# Remove everything in mail header, blank lines, and listserv info,
# Reformat to align email addresses,
# Sort by last name (second name).
tr \\r \\n \
| tr -d '[\302\240]' \
| tr -s '[:blank:]' ' '  \
| sed  -e '/^[A-Z][a-z]*: /d' -e '/^$/d' -e '/^\*/d' -e 's/"//g' \
| gawk '{ printf "%-30s %s\n", gensub(/^[^ ]+ */, "", 1), $1 }' \
| sort -f -k2 >> list-new

# sanity check: it should have a substantial number of addresses!
# (stop now if we get empty or corrupt input!)
set count =  `grep @ list-new | wc -l`
if ( "$count" < 50 ) then
    echo 'ERROR: input does not look like what I expect.'
    echo 'list-new now contains the following (top 10 lines):'
    head list-new
    goto cleanup
endif

#############################
# Diff with last month's list
grep @ list-old > tmp.old
grep @ list-new > tmp.new
diff tmp.old tmp.new > tmp.diffs
if ( -z tmp.diffs ) then
    echo "The list of subscribers has not changed since last month." > diffs
else
    echo "List changes since last month (- left, + joined):" > diffs
    sed -e '/^[-0-9]/d' -e 's/^</-/' -e 's/^>/+/' tmp.diffs >> diffs
endif

###############################
# construct the message to send
cat >  message intro
cat >> message sep diffs
cat >> message sep notes
cat >> message sep officers
cat >> message sep partners
cat >> message sep name-changes
cat >> message sep extras
cat >> message sep list-new
cat >> message sep end

##################
# send the message

set subject="Monthly ChubberNet Update"
set extras=`extract-emails extras`

# email to the list
cat message | mutt -s "$subject" "chubbers@dartmouth.edu"

# email to each of the extras, with a custom header
foreach address ($extras)
    echo email to "$address"
    cat extras-header sep message | mutt -s "$subject" "$address"
end

#################################
# remember the list for next time
mv -f list-new list-old && git add list-old

##################
# cleanup
cleanup:
rm -f diffs tmp.{old,new,diffs}
rm -f list-new message
