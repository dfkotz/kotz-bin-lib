#!/bin/csh -f
# grab the FBFW comic strip and email it to me 
# (run daily by cron)

if ($#argv < 1) then
    echo usage: $0 email...
    exit 1
endif

set strip="For Better or For Worse"

set tmp=/tmp/comic-fbfw-$$

onintr cleanup
mkdir $tmp && cd $tmp
if ($status) then
    echo mkdir or cd failed
    goto cleanup
endif

# grab the php page for today's strip
set page="http://www.fborfw.com/strip_fix/index.php"
wget -q -k "$page"
if ($status) then
    echo wget page failed
    echo "$page"
    goto alternate
endif

# extract the image URL from the page
set img=`sed -n 's+.*src="\(https:.*/strip_fix/strips/[^"]*\)".*+\1+p' "$page:t"`

if ("$img" == "") then
    echo failed to extract img address from "$page:t"
    grep strip_fix/strips "$page:t"
    goto alternate
endif

# now download the image
wget -q "$img"
if ($status) then
    echo wget img failed
    echo "$img"
    goto alternate
endif

## show me the image (for debugging)
#open $img:t
#sleep 1
#goto cleanup

## save the image (for debugging)
#cp $img:t ~/Desktop/
#if ($status) then
#    echo cp failed
#    goto cleanup
#endif

# mail me the image
echo "$page" | mutt -s "Comic strip: $strip" -a $img:t -- $argv:q
if ($status) then
    echo mutt failed when sending mail with embedded image
    goto cleanup
endif

# success!
goto cleanup

#####################################################################
# if there are failures above, send a message without embedded image.
alternate:
echo "Today's strip: $page" | mutt -s "Comic strip: $strip" -- $argv[2-]:q
if ($status) then
    echo mutt failed when sending alternate mail
    goto cleanup
endif
#####################################################################

# end point for all outcomes
cleanup:
rm -rf $tmp
